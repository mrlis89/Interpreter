plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'maven-publish'
}

group 'com.arnava'
version '1.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.assertj:assertj-core:3.21.0'
    pitest 'io.github.wmaarts:pitest-mutation-testing-elements-plugin:0.4.0'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mrlis89/Interpreter")
            credentials {
                username = 'mrlis89'
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

pitest {
    targetClasses = ["com.arnava.interpreter.*"]
    junit5PluginVersion = "0.15"
    threads = 8
    outputFormats= ["HTML2"]
    testStrengthThreshold.set(80)
    coverageThreshold.set(95)
}

jacocoTestReport {
    reports {
        csv.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
               minimum = 0.90
            }
        }
    }
}
